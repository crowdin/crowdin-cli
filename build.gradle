import com.github.spotbugs.snom.SpotBugsReport

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'jacoco'
    id "com.github.spotbugs" version "4.4.4"
}

apply plugin: 'checkstyle'

group 'com.crowdin'
version '3.5.3'

sourceCompatibility = 1.8

if (!hasProperty('mainClass')) {
    ext.mainClass = 'com.crowdin.cli.Cli'
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io'}
    maven {
        url "http://repo1.maven.org/maven2"
    }
}

dependencies {
    implementation 'org.json:json:20160212'
    implementation 'commons-cli:commons-cli:1.3.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.5'
    implementation 'org.apache.logging.log4j:log4j-core:2.5'
    implementation 'org.yaml:snakeyaml:1.17'
    implementation 'commons-io:commons-io:2.5'

    implementation 'org.glassfish.jersey.core:jersey-client:2.28'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:2.28'
    implementation 'org.glassfish.jersey.media:jersey-media-multipart:2.28'
    implementation 'javax.activation:activation:1.1.1'

    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'net.lingala.zip4j:zip4j:1.3.2'
    implementation 'net.ricecode:string-similarity:1.0.0'
    implementation 'com.github.fge:json-patch:1.9'
    compile 'io.github.cdimascio:dotenv-java:2.2.0'

    implementation 'org.apache.httpcomponents:httpclient:4.5.3'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

    implementation 'info.picocli:picocli:4.6.1'

    implementation 'com.github.crowdin:crowdin-api-client-java:1.3.4'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:2.1.0'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Crowdin CLI',
                'Implementation-Version': archiveVersion,
                'Main-Class': 'com.crowdin.cli.Cli'
    }
}

shadowJar {
    setArchivesBaseName('crowdin-cli')
    getArchiveClassifier().set('')
    minimize {
        exclude(dependency('org.apache.httpcomponents:httpclient'))
        exclude(dependency('com.fasterxml.jackson.core:jackson-databind'))
        exclude(dependency('info.picocli:picocli'))
    }
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/reports")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        xml.destination file("${buildDir}/coverage.xml")
    }

    getExecutionData().setFrom("$buildDir/jacoco/test.exec")
}

/////////////////////////
// STATIC ANALYSIS //////
/////////////////////////

spotbugs {
    ignoreFailures = false
    excludeFilter = file("config/spotbugs/excludeFilter.xml")
}
spotbugsMain {
    onlyIf {project.hasProperty('staticAnalysisMarker')}
    reports(({
        html {
            enabled = true
            destination = file("$buildDir/reports/staticAnalysis/spotbugs-main.html")
            stylesheet = 'fancy-hist.xsl'
        }
    } as Closure<NamedDomainObjectContainer<? extends SpotBugsReport>>))
}
spotbugsTest {
    onlyIf {project.hasProperty('staticAnalysisMarker')}
    reports(({
        html {
            enabled = true
            destination = file("$buildDir/reports/staticAnalysis/spotbugs-test.html")
            stylesheet = 'fancy-hist.xsl'
        }
    } as Closure<NamedDomainObjectContainer<? extends SpotBugsReport>>))
}

checkstyle {
    toolVersion '8.34'
    configFile file("config/checkstyle/checkstyle.xml")
}
checkstyleMain {
    onlyIf {project.hasProperty('staticAnalysisMarker')}
    source ='src/main/java'
    reports(({
        html {
            enabled = true
            destination = file("$buildDir/reports/staticAnalysis/checkstyle-main.html")
        }
        xml.enabled false
    }))
}
checkstyleTest {
    onlyIf {project.hasProperty('staticAnalysisMarker')}
    source ='src/test/java'
    reports(({
        html {
            enabled = true
            destination = file("$buildDir/reports/staticAnalysis/checkstyle-test.html")
        }
        xml.enabled false
    }))
}

task staticAnalysis(type: GradleBuild) {
    tasks = ['checkstyleMain', 'checkstyleTest', 'spotbugsMain', 'spotbugsTest']
}


